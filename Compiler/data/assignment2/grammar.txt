prog 			-> #BEGIN_prog #BEGIN_classDeclList classDeclList #END_classDeclList #BEGIN_funcDefList funcDefList #END_funcDefList program #BEGIN_funcBody funcBody #END_funcBody ; #END_prog
classDeclList		-> classDecl classDeclList
classDeclList		-> EPSILON
funcDefList 		-> funcDef funcDefList
funcDefList 		-> EPSILON
classDecl 		-> #BEGIN_classDecl class #MAKE_NODE id #BEGIN_inheritedList inheritedList #END_inheritedList { #BEGIN_memberList memberList #END_memberList } ; #END_classDecl
inheritedList		-> : #MAKE_NODE id idInClassDeclList
inheritedList		-> EPSILON
idInClassDeclList	-> , #MAKE_NODE id idInClassDeclList
idInClassDeclList	-> EPSILON
memberList		-> type #MAKE_NODE id memberListNew
memberList		-> EPSILON
memberListNew		-> #BEGIN_varDecl varDecl #END_varDecl memberList
memberListNew		-> #BEGIN_funcDecl funcDecl #END_funcDecl funcInClass
funcInClass			-> type #MAKE_NODE id funcInClassNew
funcInClass			-> EPSILON
funcInClassNew		-> #BEGIN_funcDecl funcDecl #END_funcDecl funcInClass
funcDecl 			-> #BEGIN_fParamsList ( fParamsList ) ; #END_fParamsList
funcHead 			-> type funcHeadId #BEGIN_fParamsList ( fParamsList ) #END_fParamsList
funcHeadId 			-> #MAKE_NODE id scopeSpec
scopeSpec 		-> #BEGIN_scopeSpec sr #END_scopeSpec #MAKE_NODE id
scopeSpec 		-> EPSILON
funcDef 			-> #BEGIN_funcDef funcHead funcBody ; #END_funcDef
funcBody 			-> #BEGIN_funcBody { varStatinFunc } #END_funcBody
varStatinFunc		-> varStat varStatinFunc
varStatinFunc		-> varStatNew varStatinFuncNew
varStatinFunc		-> EPSILON
varStatinFuncNew	-> varStatNew varStatinFuncNew
varStatinFuncNew	-> EPSILON
varStat				-> typeNew #MAKE_NODE id #BEGIN_varDecl varDecl #END_varDecl
varStatNew			-> #MAKE_NODE id varStatTail
varStatNew			-> statementOther
varStatTail			-> #MAKE_NODE id #BEGIN_varDecl varDecl #END_varDecl
varStatTail			-> indiceList idnestList assignStatTail ;
varStatTail			-> ( aParams ) idnestList assignStatTail ;
assignStatTail		-> assignOp expr
typeNew				-> #MAKE_NODE float
typeNew				-> #MAKE_NODE int
statementOther		-> if ( expr ) then statBlock else statBlock ;
statementOther		-> for ( type id assignOp expr ; relExpr ; assignStat ) statBlock ;
statementOther		-> get ( variable ) ;
statementOther		-> put ( expr ) ;
statementOther		-> return ( expr ) ;
statementList 		-> statement statementList
statementList 		-> EPSILON
varDecl 			-> #BEGIN_arraySizeList arraySizeList ; #END_arraySizeList
arraySizeList		-> arraySize arraySizeList
arraySizeList		-> EPSILON
statement 			-> assignStat ;
statement 			-> if ( expr ) then statBlock else statBlock ;
statement 			-> for ( type id assignOp expr ; relExpr ; assignStat ) statBlock ;
statement 			-> get ( variable ) ;
statement 			-> put ( expr ) ;
statement 			-> return ( expr ) ;
assignStat 			-> variable assignOp expr
statBlock 			-> { statementList }
statBlock 			-> statement
statBlock 			-> EPSILON
expr 				-> arithExpr exprNew
exprNew 			-> EPSILON
exprNew 			-> relOp arithExpr
relExpr 			-> arithExpr relOp arithExpr
arithExpr 			-> term arithExprNew
arithExprNew 		-> addOp term arithExprNew
arithExprNew 		-> EPSILON
sign 				-> +
sign 				-> -
term 				-> factor termNew
termNew 			-> multOp factor termNew
termNew 			-> EPSILON
factor 				-> varFunc
factor 				-> intNum
factor 				-> floatNum
factor 				-> ( arithExpr )
factor 				-> not factor
factor 				-> sign factor
variable 			-> id variableNew
variableNew			-> indiceList idnestList
variableNew			-> ( aParams ) idnestList
varFunc 			-> id indiceList idnestListNew varFuncTail
varFuncTail			-> ( aParams ) varFuncTail2
varFuncTail			-> EPSILON
varFuncTail2		-> idnest
varFuncTail2		-> EPSILON
idnestListNew		-> idnestNew idnestListNew
idnestListNew		-> EPSILON
indiceList			-> indice indiceList
indiceList			-> EPSILON
idnestNew 			-> . idnestNewTail
idnestNewTail		-> id indiceList
idnestNewTail		-> ( aParams )
indice 				-> [ arithExpr ]
idnestList			-> idnest idnestList
idnestList			-> EPSILON
idnest 				-> . id indiceList
arraySize 			-> [ #MAKE_NODE intNum ]
type 				-> #MAKE_NODE int
type 				-> #MAKE_NODE float
type 				-> #MAKE_NODE id
fParamsList 			->  #BEGIN_fParam type #MAKE_NODE id #BEGIN_arraySizeList arraySizeList #END_arraySizeList #END_fParam fParamsTailList
fParamsList 			-> EPSILON
fParamsTailList		-> #BEGIN_fParam fParamsTail #END_fParam fParamsTailList
fParamsTailList		-> EPSILON
aParams 			-> expr aParamsTailList
aParams 			-> EPSILON
aParamsTailList		-> aParamsTail aParamsTailList
aParamsTailList		-> EPSILON
fParamsTail 		-> , type #MAKE_NODE id #BEGIN_arraySizeList arraySizeList #END_arraySizeList
aParamsTail 		-> , expr
assignOp 			-> =
relOp 				-> eq
relOp 				-> neq
relOp 				-> lt
relOp 				-> gt
relOp 				-> leq
relOp 				-> geq
addOp 				-> +
addOp 				-> -
addOp 				-> or
multOp 				-> *
multOp 				-> /
multOp 				-> and
